# paths - path configuration

# By default, path_helper(8) does not modify MANPATH unless it is defined in
# the environment. Darwin helpfully calls path_helper from /etc/zprofile,
# which forces us to call it once again to update paths.
if [[ -x /usr/libexec/path_helper ]]; then
	export MANPATH
	eval $(/usr/libexec/path_helper -s)
fi

function collect_paths {
	local arg
	for arg; do
		if [[ -r $arg ]]; then
			while read line; do
				eval echo $line
			done <$arg
		fi
	done
}

# Avoid the NULL_GLOB option as it also affects interactive sessions;
# see: http://unix.stackexchange.com/a/26825.
path=($(collect_paths ~/.paths ~/.paths.d/*(N)) $path)
fpath=($(collect_paths ~/.fpaths ~/.fpaths.d/*(N)) $fpath)
manpath=($(collect_paths ~/.manpaths ~/.manpaths.d/*(N)) $manpath)

# vim: ft=zsh
