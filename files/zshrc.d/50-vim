# vim - Vim configuration

whence -m "*vim" &> /dev/null || return

export EDITOR=vim

# Old habits die hard...
alias vi=vim

# Client-Server
function vimremote {
	vim --servername ${VIM_SERVERNAME:-VIM} "$@"
}

# Change current directory, optionally opening the directory in the current
# window for browsing with netrw.
function vimcd {
	local arg
	local edit=0
	while getopts "e" arg; do
		case $arg in
		n)
			edit=1
			;;
		esac
	done
	shift $((OPTIND-1))

	cd $1 && vimremote --remote-expr "execute('cd ${(q)$(pwd)}')" &&
	[[ $edit != 0 ]] && vimremote --remote .
}

function vimquote {
	local arg
	for arg; do shift
		printf '"%s"' $arg
		[[ $# == 0 ]] && printf '\n' || printf ' '
	done
}

# Search for patterns on the command line using vim-grepper. This requires
# saving the working directory prior to invoking :Grepper to avoid surprises
# after the search completes. Vim quoting rules must be obvserved when
# specifying the search pattern; see eval.txt.
function vimgrep {
	local winnr=$(vimremote --remote-expr 'winnr()')
	local oldpwd=$(vimremote --remote-expr "chdir('${(q)$(pwd)}')")
	vimremote --remote-expr "execute('Grepper -noprompt -dir cwd -query $(vimquote $@)')"
	vimremote --remote-expr "execute('${winnr}wincmd w')"
	vimremote --remote-expr "chdir('${(q)oldpwd}')" &>/dev/null
}

function vimopen {
    vimremote --remote "${@:A}"
}

function vimtab {
    vimremote --remote-tab "${@:A}"
}

# vim: ft=zsh
